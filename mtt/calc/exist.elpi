% exist

type exist mttType -> (mttTerm -> mttType) -> mttType.
type pair_exist mttType -> (mttTerm -> mttType) -> mttTerm -> mttTerm -> mttTerm.
type elim_exist mttTerm -> mttType -> (mttTerm -> mttTerm -> mttTerm) -> mttTerm.

% ---- TYPING ---- %

ofType (exist B C) KIND IE
    :- ofType B KIND1 IE
    ,  (pi x\ locDecl x B => ofType (C x) KIND2 IE)
    ,  pts_for KIND1 KIND2 KIND
    .

of (pair_exist B C BB CC) (exist B C) (pair_exist B C BB' CC') IE 
    :- spy(ofType (exist B C) _ IE)
    ,  spy(isa BB B BB' IE)
    ,  spy (isa CC C_BB CC' IE)
    ,  pi bb\ (locDecl bb B, copy bb BB) => spy (conv (C bb) C_BB)
    .

of (elim_exist Pair M MM) M (elim_exist Pair' M MM') IE 
    :- spy(isa Pair (exist B C) Pair' IE)
    ,  spy(isaType M propc IE)
    ,  (pi x \ pi y \ locDecl x B => locDecl y (C x) =>  
                spy(isa (MM x y) M (MM' x y) IE))
    .

% ---- REDUCTION ---- %

hstep (elim_exist W _M MM) Res
    :- hnf W (pair_exist _B _C Bb Cc)
    ,  subst MM [Bb, Cc] Res'
    ,  hnf Res' Res
    .

% ---- CONVERSION ---- %

dconv (pair_exist B C Bb Cc) (pair_exist B' C' Bb' Cc')
    :- (conv B B')
    ,  (pi x\ locDecl x B => conv (C x) (C' x))
    ,  (conv Bb Bb')
    ,  (conv Cc Cc')
    .

dconv (elim_exist Pair M Mm) (elim_exist Pair' M' Mm') 
    :- (conv Pair Pair')
    ,  (isa Pair (exist B C) _ IE_)
    ,  (pi x\ pi y\ locDecl x B => locDecl y (C x)
        => conv (Mm x y) (Mm' x y))
    ,  (conv M M')
    .

dconv (exist B C) (exist B' C') 
    :- (conv B B')
    ,  (pi x\ locDecl x B => conv (C x) (C' x))
    .


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%                                 ESTRAZIONE                                 %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% Starification

starify_type (exist B C) LibMap Result
    :- starify_type B LibMap B'
    ,  ((name C, C' = C)
        , !; pi b\ locDecl b B => starify_type (C b) LibMap (C' b))
    ,  spy(Result = (exist B' C'))
    ,  !
    .

starify_term (pair_exist B C BB CC) LibMap Result 
    :- starify_term BB LibMap BB'
    ,  starify_term CC LibMap CC'
    ,  starify_type (exist B C) LibMap (exist B' C')
    ,  spy(Result = (pair_exist B' C' BB' CC'))
    ,  !
    .

starify_term (elim_exist Pair M MM) LibMap Result
    :- starify_term Pair LibMap Pair'
    ,  isa Pair (exist B C) @level
    ,  starify_type M LibMap M'
    ,  ((pi b\ locDecl b B => (name (MM b), MM' = MM))
        , !; pi b\ pi c\ locDecl b B => locDecl c (C b) => starify_term (MM b c) LibMap (MM' b c))
    ,  Result = (elim_exist Pair' M' MM')
    ,  !
    .

%% Extraction

collapse_type (exist B C) Result _ []
    :- spy(conv B extractor_singleton)
    ,  spy(conv C (_\extractor_singleton))
    ,  spy(Result = extractor_singleton)
    ,  !
    .

extract_type (exist B C) Result Context SetList
    :- Context = (context Vars VarMap LibMap)
    ,  spy(extract_type B B1 Context BList)
    ,  spy(ofType B Kind @level)
    ,  spy(is_informative Kind NeedsExtraction)
    ,  spy((NeedsExtraction = false, B' = B1), !; B' = extractor_singleton)
    ,  ((name C, map_var C VarMap C', CList = [])
        , !; spy(pi b\ locDecl b B => pi d\ locDecl d B' =>
            extract_type (C b) (C' d)
            (context [var_term _ d|[var_term _ b|Vars]] [var_mapping b d|VarMap] LibMap)
            CList))
    ,  concat [CList, BList] SetList
    ,  spy(collapse_type (exist B' C') Result Context [])
    ,  !
    .

%% 1 -> 1 -> 1 x 1  ==>  1
collapse_term (pair_exist B C BB CC) Result _ []
    :- isa (pair_exist B C BB CC) (exist extractor_singleton (_\ extractor_singleton)) @level
    ,  Result = extractor_star
    ,  !
    .

%% A' -> 1 -> A' x 1  ==>  A' -> 1 -> 1 x 1
extract_term (pair_exist B C BB CC) Result Context SetList
    :- Context = (context Vars VarMap LibMap)
    ,  spy(extract_type B B1 Context BList)
    ,  spy(extract_term BB BB1 Context BBList)
    ,  spy(extract_collapsing_input Context BB1 BB' BB1List)
    ,  spy((BB' = extractor_star, B' = extractor_singleton), !; B' = B1)
    ,  ((name C, map_var C VarMap C', CList = [])
        , !; spy(pi b\ locDecl b B => pi d\ locDecl d B' =>
            extract_type (C b) (C' d)
            (context [var_term _ d|[var_term _ b|Vars]] [var_mapping b d|VarMap] LibMap)
            CList))
    ,  spy(extract_term CC CC' Context CCList)
    ,  concat [BB1List, CCList, BBList, CList, BList] SetList
    ,  spy(collapse_term (pair_exist B' C' BB' CC') Result Context [])
    ,  !
    .

%% 1 -> 1  ==>  1
collapse_term (elim_exist Pair M _MM) Result _ []
    :- isa Pair extractor_singleton @level
    ,  conv M extractor_singleton
    ,  Result = extractor_star
    ,  !
    .

%% 1 x 1 -> 1  ==>  1
collapse_term (elim_exist Pair M _MM) Result _ []
    :- isa Pair (exist extractor_singleton (_\ extractor_singleton)) @level
    ,  conv M extractor_singleton
    ,  Result = extractor_star
    ,  !
    .

extract_term (elim_exist Pair M MM) Result Context SetList
    :- Context = (context Vars VarMap LibMap)
    ,  spy(extract_term Pair Pair' Context PairList)
    ,  spy(isa Pair (exist B C) @level)
    ,  ((isa Pair' extractor_singleton @level
        ,    B' = extractor_singleton, C' = (_\extractor_singleton))
        , !; isa Pair' (exist B' C') @level)
    ,  spy(extract_type M M' Context MList)
    ,  ((pi b\ locDecl b B => pi d\ locDecl d B' =>
            (name (MM b), map_var (MM b) VarMap (MM' d), MMList = []))
        , !; spy(pi b\ pi c\ locDecl b B => locDecl c (C b) => 
             pi d\ pi e\ locDecl d B' => locDecl e (C' d) =>
                extract_term (MM b c) (MM' d e)
                (context [var_term _ e|[var_term _ d|[var_term _ c|[var_term _ b|Vars]]]]
                         [var_mapping c e|[var_mapping b d|VarMap]]
                         LibMap)
                MMList))
    ,  spy(collapse_term (elim_exist Pair' M' MM') Result Context ElimList)
    ,  concat [ElimList, MMList, MList, PairList] SetList
    ,  !
    .
