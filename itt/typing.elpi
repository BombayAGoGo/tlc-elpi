kind term type. % everything is a term

% ------------------------ %
% ---- TYPE INFERENCE ---- %
% ------------------------ %

type of term -> term -> term -> o.
mode (of i o o).
of X T Y :- print (of X T Y), fail. 
of (uvar as X) T Y :- !, declare_constraint (isa X T Y) [X].

% ----------------------- %
% ---- TYPE CHECKING ---- %
% ----------------------- %

type isa term -> term -> term -> o.
mode (isa i o o).
isa X T Y :- print (isa X T Y), fail. 
isa (uvar as X) T Y :- !, declare_constraint (isa X T Y) [X].

% -------------- %
% ---- DOTS ---- %
% -------------- %

isa (app Lam dots) T O :- isa Lam T O.
isa (app Lam dots) T O :- !,
    of Lam TY _,
    steps TY (prod _ _),
    isa (app (app Lam Arg) dots) T O.


isa Te Ty OTe :-
    of Te Ty' OTe,
    conv Ty Ty'.

of_type T :- print (of_type T), fail.

% --------------------------- %
% ---- CARTESIAN PRODUCT ---- %
% --------------------------- %

% FORMATION
type prod term -> (term -> term) -> term.
macro @arrow A B :- prod A (x\ B).

% TYPE
of_type (prod A B) :- of_type A, pi x \ of x A x => of_type (B x).

% INTRODUCTION
type lam string -> term -> (term -> term) -> term.
of (lam Name A Bd) (prod A B) (lam Name A Bd') :- pi x\ isa x A x => isa (Bd x) (B x) (Bd' x).

% ELIMINATION
type app term -> term -> term.
of (app Lam Arg) B_Arg (app Lam' Arg') :-
    isa Lam (prod A B) Lam',
    isa Arg A Arg',
    B_Arg = B Arg.

% ------------------------ %
% ---- DISJOINT UNION ---- %
% ------------------------ %

% FORMATION
type sum term -> (term -> term) -> term.
macro @cross A B :- sum A (x\ B).

% TYPE
of_type (sum A B) :- of_type A, pi x \ of x A x => of_type (B x).

% INTRODUCTION
type pair term -> term -> term.
of (pair Left Right) (sum A B) (pair Left' Right') :- 
    isa Left A Left',
    isa Right (B Left) Right'.

% ELIMINATION
% Update: split ora unifica ma ho dovuto aggiungere conv e T x y anziche T <x, y> come da ML
type split term -> string -> string -> (term -> term -> term) -> term.
of (split Pair Name1 Name2 Func) (T_Pair) (split Pair' Name1 Name2 Func') :- 
    isa Pair (sum A B) Pair',
    conv Pair (pair X Y),
    pi x y\ (isa x A x, isa y (B x) y) => isa (Func x y) (T x y) (Func' x y),
    T_Pair = T X Y.

type fst term -> term.
of (fst Pair) A (fst Pair') :-
    of Pair (sum A B) Pair'.

type snd term -> term.
of (snd Pair) B_Fst (snd Pair') :-
    of Pair (sum A B) Pair',
    B_Fst = B (fst Pair).

% -------------------------------- %
% ---- PROPOSITIONAL EQUALITY ---- %
% -------------------------------- %

% FORMATION
type id term -> term -> term -> term.
of_type (id A X Y) :- of_type A, of X A X', of Y A Y'.

% INTRODUCTION
type refl term -> term -> term.
of (refl A X) (id A X Y) (refl A X') :- of X A X', of Y A _, conv X Y.

% ------------------ %
% ---- NATURALS ---- %
% ------------------ %

% FORMATION
type nat term.
of_type nat.

% INTRODUCTION
type zero term.
type succ term -> term.
of zero nat zero.
of (succ N) nat (succ N') :- of N nat N'.

% ELIMINATION
type natrec (term -> term) -> term -> term -> string -> string -> (term -> term -> term) -> term.
of (natrec T N ZeroElement Name1 Name2 RecFun) (T N) (natrec T N' ZeroElement' Name1 Name2 RecFun') :-
    of N nat N',
    of ZeroElement (T zero) ZeroElement',
    pi x y\ (of x nat x, of y (T x) y) => of (RecFun x y) (T (succ x)) (RecFun' x y).
