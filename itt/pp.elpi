accumulate lang.

% Pretty printing module, basic code borrowed from: https://github.com/LPCIC/elpi/blob/master/ELPI.md

mode (pp i o).

pp (lam Name T1 T2 Body) S :-
    (pi x\ pp x Name => pp (Body x) S1),
    pp T1 SType,
    S is "λ" ^ Name ^ ":" ^ SType ^ "." ^ S1.
pp (app T1 T2 Lambda Arg) S :-
    pp Lambda S1,
    pp Arg S2,
    S is "(" ^ S1 ^ " " ^ S2 ^ ")".
pp (prod T1 T2) S :-
    pp T1 S1,
    (pi x\ pp x S1 => pp (T2 x) S2),
    S is "∏(" ^ S1 ^ ", " ^ S2 ^ ")".
pp (pair Tl Tr El Er) S :-
    pp El Sl,
    pp Er Sr,
    S is "<" ^ Sl ^ ", " ^ Sr ^ ">".
pp (split Tl Tr T Pair VarName1 VarName2 Func) S :-
    pp Pair SPair,
    pi x y\ (pp x VarName1, pp y VarName2) => pp (Func x y) SFunc,
    S is "E(" ^ SPair  ^ ", (" ^ VarName1 ^ ", " ^ VarName2 ^ ")" ^ SFunc ^ ")".
pp (fst Pair) S :-
    pp Pair SPair,
    S is "π₁(" ^ SPair ^ ")".
pp (snd Pair) S :-
    pp Pair SPair,
    S is "π₂(" ^ SPair ^ ")".
pp (sum Tl Tr) S :-
    pp Tl Sl,
    (pi x\ pp x Sl => pp (Tr x) Sr),
    S is "∑(" ^ Sl ^ ", " ^ Sr ^ ")".
pp zero "Z".
pp (succ N) S :-
    pp N SN,
    S is "S " ^ SN.
pp (natrec N ZeroElement VarName1 VarName2 RecFun) S :-
    pp N SN,
    pp ZeroElement SZero,
    pi x y\ (pp x VarName1, pp y VarName2) => pp (RecFun x y) SRecFun,
    S is "R(" ^ SN ^ ", " ^ SZero ^ ", " ^ "(" ^ VarName1 ^ ", " ^ VarName2 ^ ") " ^ SRecFun ^ ")".
pp nat "Nat".
pp uvar "_".
pp Term S :- term_to_string Term S. % no special rules, just print the term
