% Il tipo dei tipi del lambda-calcolo tipato semplice
kind typ type.
type arr typ -> typ -> typ.     % t1 -> t2
type prod typ -> typ -> typ.    % t1 * t2
type sum typ -> typ -> typ.     % t1 + t2


% I termini del lambda-calcolo tipato semplice alla Church
kind term type.
type app term -> term -> term.
type lam typ -> (term -> term) -> term.
% product
type pair term -> term -> term.
type fst term -> term.
type snd term -> term.
% sum
type inl typ -> term -> term.
type inr typ -> term -> term.
type case term -> term -> term -> term -> term -> term.

% esempio: lambda x:nat. x x
% diventa  lam nat (x \ app x x)

% un programma Gamma è una lista di clausole
% una clausola è una formula della logica intuizionista di ordine superiore
% ristretta alla sintassi
%   P ::= Q => P | P :- Q | P,P | true | pi X \ P | exists x \ P
%       | p t1 ... tn | X t1 ... tn
%   Q ::= P => Q | Q :- P | Q,Q | Q;Q | true | false | pi x \ Q | exists X \ Q
%       | p t1 ... tn | X t1 .. tn

%%%% REGOLE DI TIPAGGIO %%%%
type of term -> typ -> o.
mode (of i o).
of (uvar as X) T :- declare_constraint (of X T) [X].
of (app Lam N) S :- of Lam (arr T S), of N T.
of (lam T Body) (arr T S) :- pi x \ of x T => of (Body x) S.

% product
of (pair Fst Snd) (prod T1 T2) :- of Fst T1, of Snd T2.
of (fst Pair) T :- of Pair (prod T S). 
of (snd Pair) S :- of Pair (prod T S). 

% sum
of (inl T Left) (sum T S) :- of Left T.
of (inr S Right) (sum T S) :- of Right S.
of (case Sum Left X Right Y) T :- 
    of Sum (sum T1 T2), 
    (pi x \ of x T1 => of X T),
    (pi y \ of y T2 => of Y T). 
    


%%%% TEST %%%%

% composizione aka \f: A -> B \g: B -> C \x: A . g f x
main :-
    of X (arr (arr a b) (arr (arr b c) (arr a c))),
    X = lam Z F,
    (pi x \
        F x = lam Z' (F' x),
        pi y \
            F' x y = lam Z'' (F'' x y),
            pi z \
                F'' x y z = app g (A x y z),
                A x y z = app x (B x y z),
                B x y z = z).

main2 :- 
    of X (arr (arr a (arr b c)) (arr (prod a b) c)).

%%%% UTILS %%%%

%length [] 0.
%length [_|TL] L :- length TL L', L is L' + 1.

% An element is nth if its the first of the nth sublist
nth_element 1 [X|_] X.
nth_element N [_|TL] X :- 
    M is N - 1,
    nth_element M TL X.


%%%% TACTICS %%%%

% aggiungi come ipotesi che ho una \x: Z.(F x) e passa a lavorare su (F x) ricorsivamente 
tactic intro (lam Z F) Vars K :- pi x \ K [([x|Vars],(F x))].
tactic (app N) (app H G) Vars K :- nth_element N Vars H, K [(Vars, G)]. 
tactic (var N) H Vars K :- nth_element N Vars H, K [].
tactic (elim N M) () Vars K :- 
    nth_element N Vars H,
    nth_element M Vars G.


%%%% INTERACTIVE ENVIRONMENT %%%%

% interactive without script
interactive :-
    of X (arr (arr a b) (arr (arr b c) (arr a c))),
    loop [([],X)] [] [].

% interactive with script
interactive InScript :-
    of X (arr (arr a b) (arr (arr b c) (arr a c))),
    loop [([],X)] [] InScript.

% (A -> (B -> C)) -> ((A & B) -> C)
interactive' :-
    of X (arr (arr a (arr b c)) (arr (prod a b) c)),
    loop [([],X)] [] [].

intercative' InScript :-
    of X (arr (arr a (arr b c)) (arr (prod a b) c)),
    loop [([],X)] [] InScript.

% Idea: se non hai meta variabili da istanziare non fare nulla e stampa...
loop [] OutScript InScript :-
    print "Total script:" OutScript.

% Se hai metavariabili ma non hai uno script di input
loop [(Vars,X)|TL] OutScript [] :-
    print OutScript,
    print_constraints,
    print, print Vars "|-" X,
    read T,
    tactic T TE Vars (Goals \ sigma TL' \
        X = TE,
        append Goals TL TL',
        loop TL' [T|OutScript] []).

% Se hai metavariabili e hai uno script di input
loop [(Vars,X)|TL] OutScript [T|InScript] :-
    tactic T TE Vars (Goals \ sigma TL' \
        X = TE,
        append Goals TL TL',
        loop TL' [T|OutScript] InScript).

append [] L L.
append [X|XS] L [X|YS] :- append XS L YS.
