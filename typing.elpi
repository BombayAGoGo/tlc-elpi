% Il tipo dei tipi del lambda-calcolo tipato semplice
kind typ type.
type arr typ -> typ -> typ.     % t1 -> t2
type prod typ -> typ -> typ.    % t1 * t2
type sum typ -> typ -> typ.     % t1 + t2
type unit typ.
type void typ.

% I termini del lambda-calcolo tipato semplice alla Church
kind term type.
type app term -> term -> term.
type lam typ -> (term -> term) -> term.
% product
type pair term -> term -> term.
type fst term -> term.
type snd term -> term.
% sum
type inl term -> term.
type inr term -> term.
% case m of x: Some -> x + 1 | y: Nothing -> 0
type case term -> term -> term -> term -> term -> term.

% abort_A i.e. given a type we can abort
%type abort typ -> term -> term.
type abort term -> term.
% singleton, only inhabitant of unit
type singl -> term.
% TODO: not
% not ... boh
type not typ -> (term -> term) -> term.

% esempio: lambda x:nat. x x
% diventa  lam nat (x \ app x x)

% un programma Gamma è una lista di clausole
% una clausola è una formula della logica intuizionista di ordine superiore
% ristretta alla sintassi
%   P ::= Q => P | P :- Q | P,P | true | pi X \ P | exists x \ P
%       | p t1 ... tn | X t1 ... tn
%   Q ::= P => Q | Q :- P | Q,Q | Q;Q | true | false | pi x \ Q | exists X \ Q
%       | p t1 ... tn | X t1 .. tn

%%%% REGOLE DI TIPAGGIO %%%%
type of term -> typ -> o.
mode (of i o).
of (uvar as X) T :- declare_constraint (of X T) [X].
of (app Lam N) S :- of Lam (arr T S), of N T.
of (lam T Body) (arr T S) :- pi x \ of x T => of (Body x) S.

% product
of (pair Fst Snd) (prod T1 T2) :- of Fst T1, of Snd T2.
of (fst Pair) T :- of Pair (prod T S). 
of (snd Pair) S :- of Pair (prod T S). 

% sum
of (inl InL) (sum T1 T2) :- of InL T1.
of (inr InR) (sum T1 T2) :- of InR T2.
% TODO: fix
of (case Sum InL X InR Y) T :- 
    of Sum (sum T1 T2), 
    of InL T1 => of (X InL) T,
    of InR T2 => of (Y InR) T.

% ex falso
%of (abort T X) T :- of X void.
of (abort X) T :- of X void.
% neg A -> bot
of (not T B) (arr T void) :-
    pi x \ of x T => of (B x) void.
of unit true.

%%%% TEST %%%%

% composizione aka \f: A -> B \g: B -> C \x: A . g f x
main :-
    of X (arr (arr a b) (arr (arr b c) (arr a c))),
    X = lam Z F,
    (pi x \
        F x = lam Z' (F' x),
        pi y \
            F' x y = lam Z'' (F'' x y),
            pi z \
                F'' x y z = app g (A x y z),
                A x y z = app x (B x y z),
                B x y z = z).

main2 :- 
    of X (arr (arr a (arr b c)) (arr (prod a b) c)).

%%%% UTILS %%%%

%length [] 0.
%length [_|TL] L :- length TL L', L is L' + 1.

% An element is nth if its the first of the nth sublist
nth_element 1 [X|_] X.
nth_element N [_|TL] X :- 
    M is N - 1,
    nth_element M TL X.


%%%% TACTICS %%%%

% impl tactics
% return a lambda abstraction, prove (F x) 
tactic intro (lam Z F) Vars K :-
    pi x \ K [([x|Vars], (F x))].

% one step hypothetical app aka I don't have an immediate function
% ex: (A => B => C) => ((A & B) => C)
%     we'll need to do B => C, C |- C
tactic app (app H G) Vars K :-
    K [(Vars, G), (Vars, H)].

% one step concrete app aka I have a function
tactic (app N) (app H G) Vars K :- 
    nth_element N Vars H, 
    K [(Vars, G)]. 

% and tactics
tactic first (fst X) Vars K :-
    K [(Vars, X)].
tactic second (snd X) Vars K :-
    K [(Vars, X)].
tactic pair (pair A B) Vars K :-
    K [(Vars, A), (Vars, B)].

% or tactics
tactic suml (inl X) Vars K :-
    K [(Vars, X)].
tactic sumr (inr X) Vars K :-
    K [(Vars, X)].
% TODO: fix
tactic case (case Sum InL F InR G) Vars K :-
    K [(Vars, Sum), ([InL|Vars], (F InL)), ([InR|Vars], (G InR))]. 


% not and bools
tactic not (not T X) Vars K :-
    pi x \ K [(x|Vars), (F x)].
tactic exfalso C Vars K :-
    K [(Vars, false)].

% hypothesis use
tactic (var N) H Vars K :- 
    nth_element N Vars H, 
    K [].


%%%% INTERACTIVE ENVIRONMENT %%%%

% interactive without script
interactive :-
    of X (arr (arr a b) (arr (arr b c) (arr a c))),
    loop [([],X)] [] [].

% interactive with script
interactive InScript :-
    of X (arr (arr a b) (arr (arr b c) (arr a c))),
    loop [([],X)] [] InScript.

% (A -> (B -> C)) -> ((A & B) -> C)
% TODO: I don't remember what tactic is tested
interactivea :-
    of X (arr (arr a (arr b c)) (arr (prod a b) c)),
    loop [([],X)] [] [].

intercativea InScript :-
    of X (arr (arr a (arr b c)) (arr (prod a b) c)),
    loop [([],X)] [] InScript.

% (A & B -> C) -> (A -> B -> C) 
% pairs test
interactiveb :-
    of X (arr (arr (prod a b) c) (arr a (arr b c))),
    loop [([],X)] [] [].

interactiveb InScript :-
    of X (arr (arr (prod a b) c) (arr a (arr b c))),
    loop [([],X)] [] InScript.


% (A v B) -> (A -> C) -> (B -> C) -> C
% case test
interactivec :-
    of X (arr (sum a b) (arr (arr a c) (arr (arr b c) c))),
    loop [([],X)] [] [].

interactivec InScript :-
    of X (arr (sum a b) (arr (arr a c) (arr (arr b c) c))),
    loop [([],X)] [] InScript.

% suml test
interactived :-
    of X (arr a (sum a b)),
    loop [([],X)] [] [].




% ITP loop

% Idea: se non hai meta variabili da istanziare non fare nulla e stampa...
loop [] OutScript InScript :-
    print "Total script:" OutScript.

% Se hai metavariabili ma non hai uno script di input
loop [(Vars,X)|TL] OutScript [] :-
    print_constraints,
    print, print Vars "|-" X,
    read T,
    tactic T TE Vars (Goals \ sigma TL' \
        X = TE,
        append Goals TL TL',
        loop TL' [T|OutScript] []).

% Se hai metavariabili e hai uno script di input
loop [(Vars,X)|TL] OutScript [T|InScript] :-
    tactic T TE Vars (Goals \ sigma TL' \
        X = TE,
        append Goals TL TL',
        loop TL' [T|OutScript] InScript).

append [] L L.
append [X|XS] L [X|YS] :- append XS L YS.
