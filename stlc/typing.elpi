% un programma Gamma è una lista di clausole
% una clausola è una formula della logica intuizionista di ordine superiore
% ristretta alla sintassi
%   P ::= Q => P | P :- Q | P,P | true | pi X \ P | exists x \ P
%       | p t1 ... tn | X t1 ... tn
%   Q ::= P => Q | Q :- P | Q,Q | Q;Q | true | false | pi x \ Q | exists X \ Q
%       | p t1 ... tn | X t1 .. tn

%type of term -> typ -> o.
%mode (of i o o).
%of (uvar as X) T :- declare_constraint (of X T) [X].

% NOTE: of and isa basic behaviour is described in commons/typing.elpi

% ------------------------ %
% ---- TYPE INFERENCE ---- %
% ------------------------ %

% arrow
of (app Lam N) S (app Lam' N') :- isa Lam (arr T S) Lam', isa N T N'.
of (lam T F) (arr T S) (lam T F') :- pi x \ of x T x => isa (F x) S (F' x).

% product
of (pair Fst Snd) (prod T1 T2) (pair Fst' Snd') :- isa Fst T1 Fst', isa Snd T2 Snd'.
of (fst Pair) T (fst Pair') :- isa Pair (prod T S) Pair'.
of (snd Pair) S (snd Pair') :- isa Pair (prod T S) Pair'.

% sum
of (inl T2 InL) (sum T1 T2) (inl T2 InL') :- isa InL T1 InL'.
of (inr T1 InR) (sum T1 T2) (inr T1 InR') :- isa InR T2 InR'.
of (case M F G) T (case M' F' G') :-
    isa M (sum T1 T2) M',
    (pi u \ isa u T1 u => isa (F u) T (F' u)),
    (pi v \ isa v T2 v => isa (G v) T (G' v)).

% top e bottom
of singleton unit singleton.
of (abort T X) T (abort T X') :- isa X void X'.

% nats
of zero nat zero.
of (succ N) nat (succ N') :- isa N nat N'.
of (natrec N ZE Fun) C (natrec N' ZE' Fun') :-
    isa N nat N',
    isa ZE C ZE',
    pi x y\ [isa x nat x, isa y C y] => isa (Fun x y) C (Fun' x y).

of tt bool tt.
of ff bool ff.
of (ite Cond Then Else) T (ite Cond' Then' Else') :-
    isa Cond bool Cond',
    isa Then T Then',
    isa Else T Else'. 

% -------------------------- %
% ---- ELABORATOR TERMS ---- %
% -------------------------- %

% named to unnamed
of (lam N T F) Arr (lam N T F') :- isa (lam T F) Arr (lam T F').
of (case N1 N2 P F G) C (case N1 N2 P' F' G') :- isa (case P F G) C (case P' F' G').
isa (app Lam dots) T O :- isa Lam T O.
isa (app Lam dots) T O :- !,
    isa Lam (arr _ _) _,
    isa (app (app Lam Arg) dots) T O.


% Coercion library + judgement
%coerce bool nat (x\ ite x (succ zero) zero).

% If you can't type a term you
% can try to coerce it as a
% last resort
%isa Term Type O :-
%    isa Term Type' _,
%    coerce Type' Type F, % e se ci sono piu' coercion?
%    subst F [Term] Coerced,
%    isa Coerced Type O.
