% -------------------------- %
% ---- SUBSTITION RULES ---- %
% -------------------------- %

% XXX: copy and conv rules are VERY similar
% but copy doesn't have i i mode i.e. it lets
% you instantiate... is there a better way
% to do this?

% TODO: try to fix cases where copy predicates
% are not in pattern fragment

% ---- Copy ---- %

copy A B :- print (copy A B), fail. % Debug

copy (lam _ F) (lam _ F') :- pi x\ copy (F x) (F' x).
copy (app A1 B1) (app A2 B2) :- copy A1 A2, copy B1 B2.

copy (pair M1 N1) (pair M2 N2) :- copy M1 M2, copy N1 N2.

copy (fst P1) (fst P2) :- copy P1 P2.
copy (snd P1) (snd P2) :- copy P1 P2.

copy (inl _ S1) (inl _ S2) :- copy S1 S2.
copy (inr _ S1) (inr _ S2) :- copy S1 S2.

copy (case M1 F1 G1) (case M2 F2 G2) :-
    copy M1 M2,
    pi x\ copy (F1 x) (F2 x),
    pi x\ copy (G1 x) (G2 x).

copy zero zero.
copy (succ N1) (succ N2) :- copy N1 N2.
copy (natrec N1 ZE1 F1) (natrec N2 ZE2 F2) :-
    copy N1 N2,
    copy ZE1 ZE2,
    pi x y\ copy (F1 x y) (F2 x y).

copy tt tt.
copy ff ff.
copy (ite C1 T1 E1) (ite C2 T2 E2) :-
    copy C1 C2,
    copy T1 T2,
    copy E1 E2.

copy X X.
%copy X Y :- hcopy X Y.
%copy X Y :- hcopy Y X.
%copy X Y :- hcopy X Z, hcopy Z Y.

/*
% male
foo (lambda F) ... :- pi x \ hyp x => foo (F x).
foo (app T1 T2) ... :- foo T1, foo T2.

% CPS e ci rimani
foo (lambda F) ... K :- pi x \ hyp x => foo (F x) K.
foo (app T1 T2) ... K :- foo T1 (z \ foo T2 (K z)).

% no CPS. Esempio tipico:  let 5 (x \ T x)
foo (lambda T F) (syntactic_hyp T O2) :- pi x \ hyp T x => foo (F x) (O2 x).
foo (app T1 T2) (comb O1 O2) :- foo T1 O1, foo T2 O2.

% combinazione delle due: la continuazione prende le variabili
foo (lambda F) Vars K :- pi x \ hyp x => foo (F x) [x|Vars] K.
foo (app T1 T2) Vars K :- foo T1 Vars (Vars' \ z \ foo T2 Vars' (K z)).
pippo I O :- foo I [] (Vars \ Oa \ put_letins Vars Oa O).
put_letins [] O O.
put_letins [X|XS] Oa (syntactic_hyp T O) :- hyp X T, put_letins XS Oa O.
*/

mode (rcopy i i i i).
%rcopy A B C D :- print (rcopy A B C D), fail.
rcopy V X [] Clause :- copy V X => Clause.
rcopy V (uvar as X) [(uvar K L)|TS] Clause :- !, 
    pi x\ rcopy x K L (rcopy V (X x) TS Clause).
rcopy V (uvar as X) [T|TS] Clause :- 
    pi x\ copy x T => rcopy V (X x) TS Clause.

mode (subst i i i).
%subst M T S :- print (subst M T S), fail. % Debug
subst M [] S :- copy M S.
subst M [(uvar K L)|TS] S :- !,
    pi x\ rcopy x K L (subst (M x) TS S).
subst M [T|TS] S :-
    pi x\ copy x T => subst (M x) TS S.
